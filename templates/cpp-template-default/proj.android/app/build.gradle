plugins {
    id 'com.android.application'
}

configurations.all {
    resolutionStrategy {
        force "androidx.appcompat:appcompat:1.6.1"
        force "androidx.appcompat:appcompat-resources:1.6.1"
        force "androidx.core:core-ktx:1.10.1"
        force "androidx.activity:activity:1.7.2"
        force "androidx.transition:transition:1.4.1"
        force "androidx.dynamicanimation:dynamicanimation:1.0.0"
        force "androidx.annotation:annotation-experimental:1.3.1"
        force "com.google.android.material:material:1.9.0"
    }

    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7"
    exclude group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8"
}

android {
    namespace "org.cocos2dx.hellocpp"
    compileSdkVersion PROP_COMPILE_SDK_VERSION.toInteger()

    defaultConfig {
        applicationId "org.cocos2dx.hellocpp"
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 1
        versionName "1.0"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }

        externalNativeBuild {
            cmake {
                targets 'HelloCpp'
                arguments "-DCMAKE_FIND_ROOT_PATH=", "-DANDROID_STL=c++_static", "-DANDROID_TOOLCHAIN=clang", "-DANDROID_ARM_NEON=TRUE"
                cppFlags "-frtti -fexceptions -fsigned-char"
            }
        }

        ndk {
            abiFilters = []
            abiFilters.addAll(PROP_APP_ABI.split(':').collect{it as String})
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "../../Resources"
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
            version "3.30.3"
        }
    }

    signingConfigs {

       release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }

        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
        }
    }

    buildFeatures {
        prefab false
    }

    ndkVersion "25.1.8937393"
}

android.applicationVariants.all { variant ->
    def project_root_folder = "${projectDir}/../.."
    def dest_assets_folder = "${projectDir}/assets"

    // delete previous files first
    delete dest_assets_folder
    def targetName = variant.name.capitalize()
    def copyTaskName = "copy${targetName}ResourcesToAssets"
    
    tasks.register(copyTaskName) {
        copy {
            from "${buildDir}/../../../Resources"
            into "${buildDir}/intermediates/assets/${variant.dirName}"
            exclude "**/*.gz"
        }
    }
    tasks.getByName("pre${targetName}Build").dependsOn copyTaskName
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(":libcocos2dx")

    // Compatible with compileSdk 33 & AGP 8.0.2
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.appcompat:appcompat-resources:1.6.1"
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.activity:activity:1.7.2"
    implementation "androidx.transition:transition:1.4.1"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
    implementation "androidx.annotation:annotation-experimental:1.3.1"

    // fix version to prevent to take newest ver 1.13.0
    implementation "com.google.android.material:material:1.9.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"
}
